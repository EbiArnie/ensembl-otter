#!/usr/bin/perl -Tw

use strict;
use warnings;

BEGIN { use lib ($ENV{OTTER_PERL_INC} || q{}) =~ m{([^:]+)}g }
use SangerPaths qw{ core bioperl123 otter };
use Bio::Otter::Git qw{ :server_ensembl :match };

use Bio::Otter::Server::Support::Web;

# Don't show any mysql connection details to clients Outside.
my %secret_params = map { ( $_ => 1 ) }
  qw{ DBNAME DBSPEC  DNA_DBSPEC DNA_DBNAME };

# DBI params no longer go over HTTP anywhere.  Whitelist the rest.
my %good_params = map { ( $_ => 1 ) }
  qw{ READONLY ALIAS  DBNAME DBSPEC  DNA_DBSPEC DNA_DBNAME };
  # HEADCODE, TYPE, RESTRICTED : obsolete

sub get_datasets {
    my ($server) = @_;
    $server->content_type('application/json');

    # Only local users get to see mysql connection details
    my $show_details = $server->local_user;

    my %datasets;
    foreach my $dataset (@{$server->allowed_datasets}) {
        my $name   = $dataset->name;
        my $params = $dataset->ds_all_params;

        my %ds;
        foreach my $key (keys %$params) {
            unless ($good_params{$key}) {
                warn "Redacted key=$key - old species.dat?";
                next;
            }
            unless ($show_details) {
                next if $secret_params{$key};
            }
            my $lckey = lc($key);   ### Why do we bother lower casing this?
            # BOL:Client upcases it, to make property accessor names
            $ds{$lckey} = $params->{$key};
        }

        $datasets{$name} = \%ds;
    }

    return \%datasets;
}

Bio::Otter::Server::Support::Web->send_response(\&get_datasets);

__END__

=head1 NAME - get_datasets

=head1 DESCRIPTION

Returns a list of datasets to the Client. Returns a list of datasets which the
user is permitted to access, determined by L<Bio::Otter::Auth::Access>.

=head1 AUTHOR

Ana Code B<email> anacode@sanger.ac.uk

