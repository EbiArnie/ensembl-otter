#!/usr/bin/perl -Tw

use strict;
use warnings;

BEGIN { use lib ($ENV{OTTER_PERL_INC} || q{}) =~ m{([^:]+)}g }
use SangerPaths qw{ core bioperl123 otter };
use Bio::Otter::Git qw{ :server_ensembl :match };

use Bio::Otter::Server::Support::Web;

# Don't show any mysql connection details to clients Outside.
my %secret_params = map { ( $_ => 1 ) }
  qw{ DBNAME DBSPEC  DNA_DBSPEC DNA_DBNAME };

# DBI params no longer go over HTTP anywhere.  Whitelist the rest.
my %good_params = map { ( $_ => 1 ) }
  qw{ ALIAS  DBNAME DBSPEC  DNA_DBSPEC DNA_DBNAME };
  # HEADCODE, TYPE : obsolete
  # RESTRICTED : not relevant to the client

sub get_datasets {
    my ($server) = @_;

    my $xml = '';

    # Only local users get to see mysql connection details
    my $show_details = $server->local_user;

    $xml   .= qq{  <datasets>\n};
    foreach my $dataset (@{$server->allowed_datasets}) {
        my $name   = $dataset->name;
        my $params = $dataset->params;

        $xml .= qq{    <dataset name="$name">\n};

        foreach my $key (keys %$params) {
            unless ($good_params{$key}) {
                warn "Redacted key=$key - old species.dat?";
                next;
            }
            unless ($show_details) {
                next if $secret_params{$key};
            }
            my $lckey = lc($key);   ### Why do we bother lower casing this?
            $xml .= qq{      <$lckey>$params->{$key}</$lckey>\n};
        }

        $xml .= qq{    </dataset>\n};
    }
    $xml .= qq{  </datasets>\n};

    return $server->otter_wrap_response($xml);
}

Bio::Otter::Server::Support::Web->send_response(\&get_datasets);

__END__

=head1 NAME - get_datasets

=head1 DESCRIPTION

Returns a list of datasets to the Client. Returns a list of datasets which the
user is permitted to access (determined by entries in the users.txt file for
external users and restricted datasets).

=head1 AUTHOR

Ana Code B<email> anacode@sanger.ac.uk

