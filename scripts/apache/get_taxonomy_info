#!/usr/bin/perl -Tw

use strict;
use warnings;

BEGIN { use lib ($ENV{OTTER_PERL_INC} || q{}) =~ m{([^:]+)}g }
use SangerPaths qw{ core bioperl123 otter };
use Bio::Otter::Git qw{ :server_ensembl :match };

use Bio::Otter::Server::Support::Web;
use Bio::Otter::Utils::MM;

my @info_key_list = qw(
    id
    scientific_name
    common_name
    );

sub _response_line {
    # $_ is the info hashref
    my @value_list = @{$_}{@info_key_list};
    # change undef to '' to avoid warnings
    for (@value_list) { defined $_ or $_ = '' }
    my $response_line = sprintf "%s\n", join "\t", @value_list;
    return $response_line;
}

Bio::Otter::Server::Support::Web->send_response(
    sub {
        my ($server) = @_;
        my $id = $server->require_argument('id');
        my @id_list = split /,/, $id;
        my $info = Bio::Otter::Utils::MM->new->get_taxonomy_info(\@id_list);
        my $header = sprintf "# %s\n", join "\t", @info_key_list;
        my $response = join '', $header, map { _response_line } @{$info};
        return $response;
    });
