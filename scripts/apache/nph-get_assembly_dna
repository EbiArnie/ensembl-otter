#!/usr/local/bin/perl -Tw

# Author:        jgrg
# Group:         anacode
# Maintainer:    jgrg
# Last modified: $Date: 2010-01-07 15:00:56 $
# Id:            $Id: nph-get_assembly_dna,v 1.1 2010-01-07 15:00:56 jgrg Exp $
# Source:        $Source: /tmp/ENSCOPY-ENSEMBL-OTTER/scripts/apache/Attic/nph-get_assembly_dna,v $

use strict;
use warnings;
use SangerPaths qw{ core bioperl123 ensembl55 otter52 };

use Bio::Otter::ServerScriptSupport;

my $server = Bio::Otter::ServerScriptSupport->new;

my $cs         = $server->param('cs')     || 'chromosome';
my $csver      = $server->param('csver')  || (($cs eq 'chromosome') ? 'Otter' : undef);
my $name       = $server->require_argument('name');
my $type       = $server->require_argument('type');
my $start      = $server->param('start');
my $end        = $server->param('end');
my $strand     = $server->param('strand') || undef;

my $metakey    = $server->param('metakey')   || ''; # defaults to pipeline, but can be '.' or something else
my $dna_wanted = $server->param('dnawanted') || 0;

my $sdba  = $server->satellite_dba( $metakey );
my $slice = $server->get_slice($sdba, $cs, $name, $type, $start, $end, $strand, $csver);

my $output_string = '';

my $attribute_adaptor = $sdba->get_AttributeAdaptor();
my %contig_dna;

foreach my $tile (@{ $slice->project('clone') }) {

    my $proj_slice = $tile->to_Slice();
    my $clone = $proj_slice->seq_region_Slice();

    my ($embl_acc, $embl_version, $intl_clone_name);

    for my $attrib (@{ $attribute_adaptor->fetch_all_by_Slice( $clone ) }) {
        my ($code, $value) = ($attrib->code(), $attrib->value());
        if($code eq 'embl_acc') {
            $embl_acc  = $value;
        } elsif($code eq 'embl_version') {
            $embl_version  = $value;
        } elsif($code eq 'intl_clone_name') {
            $intl_clone_name  = $value;
        }
    }

    my $clone_name   = $clone->seq_region_name();
    my $clone_length = $clone->length();
    my $contig_name  = $clone_name.'.1.'.$clone_length; # assuming it always has the same structure

    $intl_clone_name ||= $clone_name; # if not defined

    $output_string .= join("\t",
        $embl_acc,
        $embl_version,
        $intl_clone_name,
        $contig_name,

        $name,
        $type,

        $tile->from_start(),
        $tile->from_end(),

        $proj_slice->start(),
        $proj_slice->end(),
        $proj_slice->strand(),

        $clone->length(),
		# only send the contig's dna once
        ($dna_wanted && !$contig_dna{$contig_name}) ? ($clone->seq()) : (),
        "\n"
    );

    $contig_dna{$contig_name} = 1;
}

$server->send_response($output_string, 1);




__END__

=head1 NAME - nph-get_assembly_dna

=head1 AUTHOR

James Gilbert B<email> jgrg@sanger.ac.uk

