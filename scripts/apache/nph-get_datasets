#!/usr/local/bin/perl -Tw

# Author:        jgrg
# Group:         anacode
# Maintainer:    jgrg
# Last modified: $Date: 2010-02-22 16:49:44 $
# Id:            $Id: nph-get_datasets,v 1.23 2010-02-22 16:49:44 jgrg Exp $
# Source:        $Source: /tmp/ENSCOPY-ENSEMBL-OTTER/scripts/apache/Attic/nph-get_datasets,v $

use strict;
use warnings;
use SangerPaths qw{ core bioperl123 ensembl56 otter52 };
use Bio::Otter::ServerScriptSupport;

# For added security we don't show mysql connection details to clients outside
# the Sanger. We only need connection details to for scripts running at Sanger.
my %secret_params = map {$_, 1} qw{
    HOST
    PORT
    USER
    PASS
    DBNAME

    DNA_HOST
    DNA_PORT
    DNA_USER
    DNA_PASS
    DNA_DBNAME
};

my $server = Bio::Otter::ServerScriptSupport->new;

my $xml;
eval {
    my $species_hash = $server->dataset_hash;
    
    # Only local users get to see mysql connection details
    my $show_details = $server->local_user;

    $xml   .= qq{  <datasets>\n};
    foreach my $dataset (keys %$species_hash) {
        my $species_subhash = $species_hash->{$dataset};

        $xml .= qq{    <dataset name="$dataset">\n};

        foreach my $key (keys %$species_subhash) {
            unless ($show_details) {
                next if $secret_params{$key};
            }
            my $lckey = lc($key);   ### Why do we bother lower casing this?
            $xml .= qq{      <$lckey>$species_subhash->{$key}</$lckey>\n};
        }

        $xml .= qq{    </dataset>\n};
    }
    $xml .= qq{  </datasets>\n};
};

if ($@) {
    $server->error_exit($@);
} else {
    $server->send_response($xml, 1);
}

1;

=pod

=head1 get_datasets

=head1 DESCRIPTION

Returns a list of datasets to the Client. Returns a list of datasets which the
user is permitted to access (determined by entries in the users.txt file for
external users and restricted datasets).

=cut
