#!/usr/local/bin/perl -Tw

# Author:        jgrg
# Group:         anacode
# Maintainer:    jgrg
# Last modified: $Date: 2007-10-09 15:04:53 $
# Id:            $Id: nph-get_sequencesets,v 1.8 2007-10-09 15:04:53 lg4 Exp $
# Source:        $Source: /tmp/ENSCOPY-ENSEMBL-OTTER/scripts/apache/Attic/nph-get_sequencesets,v $

use strict;
use SangerPaths qw{ core bioperl123 ensembl46 otter48 };

use Bio::Otter::ServerScriptSupport;

my $server = Bio::Otter::ServerScriptSupport->new;
my $xml = '';

eval {

    my $dataset = $server->require_argument('dataset');

    my $odba = $server->otter_dba();

    $server->fetch_Author_obj();  # will break if author cannot be fetched

        # mapping between subregions and chromosomes:
    my $sql = qq{
        SELECT sub.name, chr.name, count(*) cnt, group_concat(ctg.name ORDER BY subasm.asm_start SEPARATOR ',')
        FROM seq_region sub,
             assembly subasm,
             seq_region ctg,
             assembly chrasm,
             seq_region chr
        WHERE sub.coord_system_id=(SELECT coord_system_id FROM coord_system where name='subregion')
          AND sub.seq_region_id=subasm.asm_seq_region_id
          AND ctg.seq_region_id=subasm.cmp_seq_region_id
          AND ctg.coord_system_id=(SELECT coord_system_id FROM coord_system where name='contig')
          AND subasm.cmp_seq_region_id=chrasm.cmp_seq_region_id
          AND chr.seq_region_id=chrasm.asm_seq_region_id
          AND (chr.coord_system_id=(SELECT coord_system_id FROM coord_system where name='chromosome' and version='Otter')
               OR chr.seq_region_id=sub.seq_region_id)
        GROUP BY sub.name, chr.name
        ORDER BY sub.name, cnt DESC
    };

    my $sth = $odba->dbc()->prepare($sql);
    $sth->execute();

    my %sub_contig_count = ();
    my %chr_2_sub = ();
    my %sub_contigs = ();
    while( my ($sub_name, $chr_name, $cnt, $contig_list) = $sth->fetchrow() ) {
        if(!defined($sub_contig_count{$sub_name})) { # this is the first time we see it
            $sub_contig_count{$sub_name} = $cnt;
            $sub_contigs{$sub_name} = $contig_list; # just as comma-separated list
        }

        if(($cnt == $sub_contig_count{$sub_name}) && ($chr_name ne $sub_name)) {
            push @{$chr_2_sub{$chr_name}}, $sub_name;
        }
    }


    $xml   .= qq`  <dataset name="$dataset">\n`;
    $xml   .= qq`    <sequencesets>\n`;

    my $slice_adaptor     = $odba->get_SliceAdaptor();
    my $attribute_adaptor = $odba->get_AttributeAdaptor();

    foreach my $ss_slice (@{$slice_adaptor->fetch_all('chromosome', 'Otter')}) {

        my $chr_name = $ss_slice->seq_region_name();
        my $description  = ''; # default
        my $write_access = 1;  # default
        my $hidden       = 1;  # default

        # FIXME: we will need finer, by-author's-name control over visibility/writability of sequence sets

        for my $attrib (@{ $attribute_adaptor->fetch_all_by_Slice( $ss_slice ) }) {
            my ($code, $value) = ($attrib->code(), $attrib->value());
            if($code eq 'description') {
                $description  = $value;
            }elsif($code eq 'write_access') {
                $write_access = $value;
            }elsif($code eq 'hidden') {
                $hidden       = $value;
            }
        }

        $xml .= qq`      <sequenceset name="$chr_name">\n`;
        $xml .= qq`        <description>$description</description>\n`;
        $xml .= qq`        <is_hidden>$hidden</is_hidden>\n`;
        $xml .= qq`        <write_access>$write_access</write_access>\n`;

        foreach my $sub_name (@{$chr_2_sub{$chr_name}}) {
            $xml .= qq`        <subregion name="$sub_name">$sub_contigs{$sub_name}</subregion>\n`;
        }

        $xml .= qq`      </sequenceset>\n`;
    }

    $xml .= qq`    </sequencesets>\n`;
    $xml .= qq`  </dataset>\n`;
};

if ($@) {
    $server->error_exit($@);
} else {
    $server->send_response($xml, 1);
}

1;

__END__

=head1 NAME - get_sequencesets


=head1 DESCRIPTION

return xml for sequence sets

=head1 AUTHOR

Roy Storey,,,, B<email> rds@sanger.ac.uk
refactored by Sindhu K.Pillai B<email> sp1@sanger.ac.uk
Leo Gordon B<email> lg4@sanger.ac.uk - support for the new schema

