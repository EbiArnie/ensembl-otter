#!/usr/local/bin/perl -Tw

# Author:        jgrg
# Group:         anacode
# Maintainer:    jgrg

use strict;
use warnings;

use SangerPaths qw{ core };
use SangerWeb;

use IO::Socket;
use CGI;

### We avoid these for faster turnaround of requests, but have to
#   replicate equivalent functionality inline.
#
# use Bio::Otter::ServerScriptSupport;
# use Hum::Conf qw{ PFETCH_SERVER_LIST };
#
# Listed here for the benefit of greppers...


my $q;

eval {
    $q = CGI->new;

    # XXX: remove this when nph-* are gone; should we be using ServerScriptSupport.pm ?
    my $script = $ENV{SCRIPT_FILENAME} || $0;
    $q->nph(1) if $script =~ m{/nph-};
    
    my $sw = SangerWeb->new({ cgi => $q });
    die "Not authorized\n"
      unless $sw->username;
};

if ($@) {
    print $q->header(
        -status => 403,
        -type   => 'text/plain',
        );
    print $@;
    exit(1);
}

my $socket;

eval {
    # Get request string and de-taint
    my $req = $q->param('request');
    $req =~ s/\s+/ /g;
    $req =~ s/[^\w\-\.\: ]//g;

    die "Empty request\n" unless $req =~ /\S/;

    foreach my $ip_addr (qw{ pfetch.sanger.ac.uk }){
        last if $socket = IO::Socket::INET->new(
            PeerAddr => $ip_addr,
            PeerPort => 22400,
            Proto    => 'tcp',
            Type     => SOCK_STREAM,
            Timeout  => 10,
            );
    }
    die "Connect to pfetch server failed" unless $socket;
    
    print $socket $req, "\n";
};

if ($@) {
    print $q->header(
        -status => 500,
        -type   => 'text/plain',
        );
    print $@;
    exit(1);
} else {
    print $q->header(
        -status => 200,
        -type   => 'text/plain',
        );
    
    while (<$socket>) {
        print;
    }
    close $socket;
    exit(0);
}
