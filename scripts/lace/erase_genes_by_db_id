#!/usr/bin/env perl

use warnings;


### erase_genes_by_db_id

use strict;

use Bio::Otter::Lace::Defaults;
use Bio::Otter::DBUtils 'delete_genes_by_exon_id_list';

{
    my $test_flag = 0;
    my $dataset_name = undef;
    my $usage = sub { exec 'perldoc', $0 };
    
    Bio::Otter::Lace::Defaults::do_getopt(
          'dataset=s'   => \$dataset_name,
          'test!'       => \$test_flag,
          ) or die "Error processing command line";
    $usage->() unless $dataset_name and @ARGV;
    my $cl = Bio::Otter::Lace::Defaults::make_Client();
    my $ds = $cl->get_DataSet_by_name($dataset_name);
    # Make a connection to the otter database
    my $otter_db = $ds->get_cached_DBAdaptor->db_handle;
    
    
    $otter_db->begin_work;
    eval{ delete_genes_by_db_id($otter_db, @ARGV) };
    if ($@) {
        warn "Error doing delete: $@";
        $otter_db->rollback;
    }
    elsif ($test_flag) {
        $otter_db->rollback;
    }
    else {
        $otter_db->commit;
    }
}

sub delete_genes_by_db_id {
    my( $dbh, @gene_id ) = @_;
    
    my $id_str = join(',', map { "'$_'" } @gene_id);
    
    my $sql = qq{
        SELECT DISTINCT e.exon_id
        FROM transcript t
          , exon_transcript et
          , exon e
        WHERE t.transcript_id = et.transcript_id
          AND et.exon_id = e.exon_id
          AND t.gene_id in ($id_str)
        };
    my $sth = $dbh->prepare($sql);
    $sth->execute;    

    my( $exon_id );
    $sth->bind_columns(\$exon_id);
    
    my $exon_list = [];
    while ($sth->fetch) {
        push(@$exon_list, $exon_id);
    }
    print "Deleting ",scalar(@$exon_list)," distinct exon ids\n"; 
    
    delete_genes_by_exon_id_list($dbh, $exon_list);

    return;
}





__END__

=head1 NAME - erase_genes_by_db_id

=head1 AUTHOR

James Gilbert B<email> jgrg@sanger.ac.uk

