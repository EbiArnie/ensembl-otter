#!/usr/bin/env perl

### query_all_datasets

use strict;
use warnings;
use Bio::Otter::Lace::Defaults;

{
    my $sql;
    my $pipe = 0;
    Bio::Otter::Lace::Defaults::do_getopt(
        'query=s' => \$sql,
        'pipe!'   => \$pipe,
    );
    unless ($sql) {
        $sql = join(' ', <>);
    }
    die "No query" unless $sql;

    print "$sql\n";

    my $cl = Bio::Otter::Lace::Defaults::make_Client();
    foreach my $ds ($cl->get_all_DataSets) {
        printf "Querying dataset %s\n", $ds->name;
        my $otter_dba = $ds->get_cached_DBAdaptor;
        my $query_dba =
          $pipe
          ? Bio::Otter::Lace::PipelineDB::get_pipeline_DBAdaptor($otter_dba)
          : $otter_dba;
        my $sth = $query_dba->dbc->prepare($sql);
        $sth->execute;
        while (my @row = $sth->fetchrow) {
            print join("\t", @row), "\n";
        }
    }
}

__END__

=head1 NAME - query_all_datasets

=head1 SYNOPSIS

  query_all_datasets [-pipe] -query "SQL STATEMENT"

=head1 PARAMETERS

=over 4

=item B<-pipe>

Query the pipeline databases instead of the (default) loutre databases.

=item B<-query>

The SQL to send to each database.

=back

=head1 AUTHOR

James Gilbert B<email> jgrg@sanger.ac.uk

