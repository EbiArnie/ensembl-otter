#!/usr/local/bin/perl -w

### save_satellite_db

use strict;
use Getopt::Long;
use Bio::Otter::Lace::SatelliteDB;
use Bio::Otter::Lace::Defaults;

{
    my (
      $help, $key, $view,
      $sathost, $satport, $satuser, $satpass, $satdbname
      );
    my $dataset_name = 'human';
    Bio::Otter::Lace::Defaults::do_getopt(
        'h|help!'     => \$help,
        'key=s'       => \$key,
        'show'        => \$view,
        'sathost=s'   => \$sathost,
        'satport=s'   => \$satport,
        'satuser=s'   => \$satuser,
        'satpass=s'   => \$satpass,
        'satdbname=s' => \$satdbname,
        'dataset=s'   => \$dataset_name,
      )
      || usage();
    usage() if $help;
    die "Key not given" unless $key;

    my $cl       = Bio::Otter::Lace::Defaults::make_Client();
    my $ds       = $cl->get_DataSet_by_name($dataset_name);
    my $otter_db = $ds->get_cached_DBAdaptor;

    my $current =
      Bio::Otter::Lace::SatelliteDB::get_options_for_key($otter_db, $key);
    if ($view) {
        print show_current($current, $key);
        exit;
    }
    my $sat_opts = $current || {};
    $sat_opts->{'-HOST'}   = $sathost   if $sathost;
    $sat_opts->{'-PORT'}   = $satport   if $satport;
    $sat_opts->{'-USER'}   = $satuser   if $satuser;
    $sat_opts->{'-PASS'}   = $satpass   if $satpass;
    $sat_opts->{'-DBNAME'} = $satdbname if $satdbname;

    # so we can use undefined password
    delete($sat_opts->{'-PASS'}) unless $satpass;

    # Check we can connect to the Satellite database with options supplied
    eval {
        my $sat_db = Bio::EnsEMBL::DBSQL::DBAdaptor->new(%$sat_opts);
        $sat_db = undef;
    };
    if ($@) {
        die "Couldn't connect to SatelliteDB selected:\n"
          . show_current($sat_opts, 'SatelliteDB')
          . $@;
    }

    # delete the key
    Bio::Otter::Lace::SatelliteDB::remove_options_hash_for_key($otter_db, $key);

    # replace with new version
    Bio::Otter::Lace::SatelliteDB::save_options_hash($otter_db, $key,
        $sat_opts);

}

sub show_current {
    my ($settings, $name, $ret) = @_;
    $ret = "Settings for $name:\n";
    foreach my $key (keys(%$settings)) {
        $ret .= "$key   =  " . $settings->{$key} . "\n";
    }
    return $ret;
}

#---------
sub usage { exit(exec("perldoc", $0)) }

__END__

=head1 NAME - save_satellite_db

=head1 SYNOPSIS

 ./save_satellite_db -key <key> -dataset <dataset> [-show] [-satdb* options]

=head1 DESCRIPTION

    Saves the connection hash for a satellite database for 
the otter server under the B<key>.  By default it uses the options 
already stored in the database for the B<key>.

=head1 USAGE

    help           - print this pod
    key (required) - meta_key to use in the meta table
    dataset (req)  - otter dataset to use [human]
    show           - view the current settings
    sathost        - host for the satellite DB
    satport        - port for the satellite DB
    satuser        - user for the satellite DB
    satpassword    - pass for the satellite DB
    satdbname      - dbname for the satellite DB

=head1 EXAMPLE

    ./save_satellite_db -dataset zebrafish -key pipeline_db -sathost ecs2 -satport 3310 -satuser ensro

=head1 AUTHOR

James Gilbert B<email> jgrg@sanger.ac.uk

