#!/usr/local/bin/perl -w

my $pipehead = $ENV{PIPEHEAD}; # is set by the server for any GET request

use strict;
use OtterDefs;
use Bio::Otter::ServerQuery;
use Bio::Otter::ServerSide (':all');
use Bio::EnsEMBL::DBSQL::DBAdaptor;
use Bio::Otter::DBSQL::DBAdaptor;

$| = 1;

my $DEBUG=0;

my $sq = Bio::Otter::ServerQuery->new('dataset','qnames');
set_nph($sq);

# This is a 3-level hash:
# {query_name}{type_of_query}{clones_found}
#
my %qnames_types_clones = map {($_ => {})} split(',', $sq->getarg('qnames'));

my $odb = get_DBAdaptor_from_CGI_species($sq, $OTTER_SPECIES, $pipehead);

find_clones_by_qnames($sq, $odb, \%qnames_types_clones);

my $output_string = '';

for my $qname (keys %qnames_types_clones) {
    my $types_clones = $qnames_types_clones{$qname};
    if(keys %$types_clones) {
        for my $type (keys %$types_clones) {
            $output_string .= join("\t", $qname, $type, join(',', keys %{$types_clones->{$type}}))."\n";
        }
    } else {
        $output_string .= "$qname\n"; # an equivalent of asymmetric join
    }
}

send_response($sq, $output_string, 1);

1;

############## actual search subroutines: ################################
sub exons2clones {
    my ($qname, $type, $exons, $qnames_types_clones) = @_;

    server_log("'$qname' was found and contains ".scalar(@$exons)." exons.");
    foreach my $exon (@$exons) {
        my $contigid = $exon->contig->dbID();
        my $clone = $exon->contig()->clone();
        my $embl = $clone->embl_id().'.'.$clone->embl_version();

        $qnames_types_clones->{$qname}{$type}{$embl}++;
    }
}

sub find_clones_by_qnames {
    my ($sq, $dba, $qnames_types_clones) = @_;

    my $meta_con = $dba->get_MetaContainer();
    my $prefix_primary = $meta_con->get_primary_prefix() || error_exit($sq, "Missing prefix.primary in meta table");
    my $prefix_species = $meta_con->get_species_prefix() || error_exit($sq, "Missing prefix.species in meta table");

    my $gene_adaptor           = $dba->get_GeneAdaptor();
    my $genename_adaptor       = $dba->get_GeneNameAdaptor();
    my $genesyn_adaptor        = $dba->get_GeneSynonymAdaptor();
    my $geneinfo_adaptor       = $dba->get_GeneInfoAdaptor();

    my $transcript_adaptor     = $dba->get_TranscriptAdaptor();

    my $exon_adaptor           = $dba->get_ExonAdaptor();

    foreach my $qname (keys %$qnames_types_clones) {
        if(uc($qname) =~ /^$prefix_primary$prefix_species([TPGE])\d+/i){ # try stable_ids
            my $typeletter = $1;
            my $type;
            my $exons;

            eval {
                if($typeletter eq 'G') {
                    $type = 'gene_stable_id';
                    $exons = $gene_adaptor->fetch_by_stable_id($qname)->get_all_Exons();
                } elsif($typeletter eq 'T') {
                    $type = 'transcript_stable_id';
                    $exons = $transcript_adaptor->fetch_by_stable_id($qname)->get_all_Exons();
                } elsif($typeletter eq 'P') {
                    $type = 'translation_stable_id';
                    $exons = $transcript_adaptor->fetch_by_translation_stable_id($qname)->get_all_Exons();
                } elsif($typeletter eq 'E') {
                    $type = 'exon_stable_id';
                    $exons = [ $exon_adaptor->fetch_by_stable_id($qname) ];
                }
            };
                # Just imagine: they raise an EXCEPTION to indicate nothing was found. Terrific!
            if($@) {
                server_log("'$qname' looks like a stable id, but wasn't found.");
                server_log($@)if $DEBUG;
            } else {
                exons2clones($qname, $type, $exons, $qnames_types_clones);
            }
        }
        if($qname =~ /^(\w+)(?:\.(\d+))?$/) { # try clone accessions with & without version number
            my $wanted_acc = $1;
            my $wanted_version = $2;

            my $sql = qq{
                SELECT embl_acc, embl_version
                FROM clone
                WHERE embl_acc = '$wanted_acc'
            }. (defined($wanted_version) ? qq{ AND embl_version = '$wanted_version' } : '');
            warn $sql if $DEBUG;
            my $sth = $dba->prepare($sql);
            $sth->execute;
            
            server_log("trying clone accession[.version] '$qname' ");
            while (my ($embl_acc, $embl_version) = $sth->fetchrow) {
                $qnames_types_clones->{$qname}{clone_accession}{"$embl_acc.$embl_version"}++;
            }
        }

        { # try intl. clone names:
            my $sql = qq{
                SELECT embl_acc, embl_version
                FROM clone
                WHERE name = '$qname'
            };
            warn $sql if $DEBUG;
            my $sth = $dba->prepare($sql);
            $sth->execute;
            
            server_log("trying intl. clone name '$qname' ");
            while (my ($embl_acc, $embl_version) = $sth->fetchrow) {
                $qnames_types_clones->{$qname}{intl_clone_name}{"$embl_acc.$embl_version"}++;
            }
        }


        { # try gene name or synonym:
            my $exons;
            eval{
                server_log("trying gene name or synonym '$qname' ");
                my $geneNameObjList = $genename_adaptor->fetch_by_name($qname);
                my $geneSynObjList  = $genesyn_adaptor->fetch_by_name($qname);
                foreach my $geneNameObj (@$geneNameObjList, @$geneSynObjList){
                    my $geneInfoObj = $geneinfo_adaptor->fetch_by_dbID($geneNameObj->gene_info_id());    
                    $exons = $gene_adaptor->fetch_by_stable_id($geneInfoObj->gene_stable_id())->get_all_Exons();
                    exons2clones($qname, 'gene_name_or_synonym', $exons, $qnames_types_clones);
                }
            };
            if ($@){
                ## assume error was caused by not being able to create a $geneNameObjList - as name didnt exist
                server_log("no gene was found with name or synonym '$qname'"); 
                server_log($@)if $DEBUG;
            }
        }

    } # foreach $qname
}
