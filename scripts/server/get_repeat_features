#!/usr/local/bin/perl -w

use strict;
use Bio::Otter::ServerScriptSupport;
use Bio::Otter::Lace::ViaText ('%OrderOfOptions');

my $server = Bio::Otter::ServerScriptSupport->new('dataset',
    'cs','csver','name','type','start','end','strand',
    'metakey','analysis');

my $analysis = $server->getarg('analysis');

my $rfs = $server->fetch_mapped_features('repeat_feature',
            ['get_all_RepeatFeatures', $analysis] );

my @rf_optnames = @{ $OrderOfOptions{RepeatFeature} };
my @rc_optnames = @{ $OrderOfOptions{RepeatConsensus} };

my $output_string = '';

    # Stringify only the simple fields:
my %rc_seen = (); # collect the seen repeat consensus ids here
foreach my $rf (@$rfs) {
    my $rc = $rf->repeat_consensus(); # object
    my $rc_id = $rc->dbID();

    if(!exists($rc_seen{$rc_id})) { # a new one

            # output a repeat consensus line
        my @rc_optvalues = ('RepeatConsensus');
        for my $opt (@rc_optnames) {
            push @rc_optvalues, $rc->$opt() || 0;
        }
        $output_string .= join("\t", @rc_optvalues)."\n";

        $rc_seen{$rc_id} = 1;
    }
    
        # output a repeat feature line
    my @rf_optvalues = ('RepeatFeature');
    for my $opt (@rf_optnames) {
        push @rf_optvalues, $rf->$opt();
    }
    push @rf_optvalues, $rc_id;

    $output_string .= join("\t", @rf_optvalues)."\n";
}

$server->send_response($output_string, 1);

1;

