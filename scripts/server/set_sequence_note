#!/usr/local/bin/perl -w

use strict;
use OtterDefs;
use Bio::Otter::ServerScriptSupport;

my $server = Bio::Otter::ServerScriptSupport->new('dataset',
    'contig','author','email','timestamp','text', 'action');

my $contig_name = $server->require_argument('contig');
my $timestamp   = $server->require_argument('timestamp');
my $text        = $server->require_argument('text');
my $action      = $server->require_argument('action');
my $aut_name    = $server->require_argument('author');
my $aut_email   = $server->getarg('email')  || $aut_name;

my $running_headcode = $server->running_headcode();
my $odba = $server->otter_dba();
my $odbc = $running_headcode ? $odba->dbc() : $odba;

if($action=~/(change|modify|old)/) {
    $server->log("Changing a sequence_note");

    my $sth = $odbc->prepare(qq{
        UPDATE sequence_note
           SET note      = ?
         WHERE }. ($running_headcode
             ?  qq{seq_region_id = (SELECT seq_region_id FROM seq_region where name=?) }
             :  qq{contig_id     = (SELECT contig_id     FROM contig     where name=?) }
         ).qq{ AND author_id = (SELECT author_id FROM author where author_name=?)
             AND note_time = FROM_UNIXTIME(?)
    });
    my $changed = $sth->execute($text, $contig_name, $aut_name, $timestamp);
    $server->log("Changed $changed sequence_notes for contig $contig_name");

} elsif($action=~/(add|push|new)/) {
    $server->log("Adding a new sequence_note");

        # check if such an author existed:
    my $sth = $odbc->prepare(qq{
        SELECT author_id
          FROM author
         WHERE author_name = ?
    });
    $sth->execute($aut_name);

        # add him, if he didn't:
    if(! (my($aut_id) = $sth->fetchrow())) {
        $sth = $odbc->prepare(qq{
            INSERT INTO author (author_name, author_email)
                 VALUES (?, ?)
        });
        $sth->execute($aut_name, $aut_email);
    }

    $sth = $odbc->prepare( $running_headcode
    ? qq{
        UPDATE sequence_note
           SET is_current = 'no'
         WHERE seq_region_id = (SELECT seq_region_id FROM seq_region where name=?)
    } : qq{
        UPDATE sequence_note
           SET is_current = 'N'
         WHERE contig_id = (SELECT contig_id FROM contig where name=?)
    });
    my $non_curr=$sth->execute($contig_name);
    $server->log("Contig $contig_name has $non_curr non-current sequence_notes now...");

    $sth = $odbc->prepare( $running_headcode
    ? qq{
        INSERT INTO sequence_note (seq_region_id, author_id, note_time, is_current, note)
             VALUES (
                     (SELECT seq_region_id FROM seq_region where name=?),
                     (SELECT author_id FROM author where author_name=?),
                     FROM_UNIXTIME(?),
                     'yes',
                     ?
             )
    } : qq{
        INSERT INTO sequence_note (contig_id, author_id, note_time, is_current, note)
             VALUES (
                     (SELECT contig_id FROM contig where name=?),
                     (SELECT author_id FROM author where author_name=?),
                     FROM_UNIXTIME(?),
                     'Y',
                     ?
             )
    });
    my $curr=$sth->execute($contig_name, $aut_name, $timestamp, $text);
    $server->log( "... and $curr current sequence_notes");
}

$server->send_response("Done\n", 1);

1;

