#!/usr/local/bin/perl -w

my $pipehead = $ENV{PIPEHEAD}; # is set by the server for any GET request

use strict;
use OtterDefs;
use Bio::Otter::ServerQuery;
use Bio::Otter::ServerSide (':all');
use Bio::Otter::DBSQL::DBAdaptor;

$| = 1;

my $sq = Bio::Otter::ServerQuery->new('dataset',
    'contig','author','email','timestamp','text', 'action');
set_nph($sq);

my $odba = get_DBAdaptor_from_CGI_species($sq, $OTTER_SPECIES, $pipehead);
my $odbc = $pipehead ? $odba->dbc() : $odba;

my $contig_name = $sq->getarg('contig')    || error_exit($sq, "Please define 'contig' name");
my $aut_name    = $sq->getarg('author')    || error_exit($sq, "Please define 'author' name");
my $aut_email   = $sq->getarg('email')     || $aut_name;
my $timestamp   = $sq->getarg('timestamp') || error_exit($sq, "Please define 'timestamp' attribute'");
my $text        = $sq->getarg('text')      || error_exit($sq, "Please define 'text' attribute");
my $action      = $sq->getarg('action')    || error_exit($sq, "Please choose the 'action'");

if($action=~/(change|modify|old)/) {
    server_log("Changing a sequence_note");

    my $sth = $odbc->prepare(qq{
        UPDATE sequence_note
           SET note      = ?
         WHERE }. ($pipehead
             ?  qq{seq_region_id = (SELECT seq_region_id FROM seq_region where name=?) }
             :  qq{contig_id     = (SELECT contig_id     FROM contig     where name=?) }
         ).qq{ AND author_id = (SELECT author_id FROM author where author_name=?)
             AND note_time = FROM_UNIXTIME(?)
    });
    my $changed = $sth->execute($text, $contig_name, $aut_name, $timestamp);
    server_log("Changed $changed sequence_notes for contig $contig_name");

} elsif($action=~/(add|push|new)/) {
    server_log("Adding a new sequence_note");

        # check if such an author existed:
    my $sth = $odbc->prepare(qq{
        SELECT author_id
          FROM author
         WHERE author_name = ?
    });
    $sth->execute($aut_name);

        # add him, if he didn't:
    if(! (my($aut_id) = $sth->fetchrow())) {
        $sth = $odbc->prepare(qq{
            INSERT INTO author (author_name, author_email)
                 VALUES (?, ?)
        });
        $sth->execute($aut_name, $aut_email);
    }

    $sth = $odbc->prepare( $pipehead
    ? qq{
        UPDATE sequence_note
           SET is_current = 'N'
         WHERE contig_id = (SELECT contig_id FROM contig where name=?)
    } : qq{
        UPDATE sequence_note
           SET is_current = 'no'
         WHERE seq_region_id = (SELECT seq_region_id FROM seq_region where name=?)
    });
    my $non_curr=$sth->execute($contig_name);
    server_log("Contig $contig_name has $non_curr non-current sequence_notes now...");

    $sth = $odbc->prepare( $pipehead
    ? qq{
        INSERT INTO sequence_note (contig_id, author_id, note_time, is_current, note)
             VALUES (
                     (SELECT contig_id FROM contig where name=?),
                     (SELECT author_id FROM author where author_name=?),
                     FROM_UNIXTIME(?),
                     'Y',
                     ?
             )
    } : qq{
        INSERT INTO sequence_note (seq_region_id, author_id, note_time, is_current, note)
             VALUES (
                     (SELECT seq_region_id FROM seq_region where name=?),
                     (SELECT author_id FROM author where author_name=?),
                     FROM_UNIXTIME(?),
                     'yes',
                     ?
             )
    });
    my $curr=$sth->execute($contig_name, $aut_name, $timestamp, $text);
    server_log( "... and $curr current sequence_notes");
}

send_response($sq, "Done\n", 1);

