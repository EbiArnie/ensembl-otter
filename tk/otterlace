#!/usr/bin/env perl

use strict;
use warnings;

### otterlace

use CanvasWindow::MainWindow;
use MenuCanvasWindow::SpeciesListWindow;
use EditWindow::Password;
use Bio::Otter::Lace::Client;
use Bio::Otter::Git;
use Bio::Otter::Error;
use POSIX ();
use Log::Log4perl;
use Try::Tiny;

{
    # Become leader of process group so that "kill -$$" at end works
    setpgrp();

    Bio::Otter::Lace::Defaults::do_getopt();
    if (@ARGV) {
        my $name = $ENV{OTTERLACE_RAN_AS};
        if (defined $name) {
            $name =~ s{ -> .*}{};
        } else {
            $name = $0;
        }
        die "$name: options (@ARGV) were not understood"; # RT#334602
    }
    my $cl = Bio::Otter::Lace::Defaults::make_Client();
    $cl->make_log_file('otterlace');
    # After this, we are writing logfile
    my $logger = Log::Log4perl->get_logger('otter.main');

    $cl->cleanup_sessions;
    $cl->cleanup_log_dir('otterlace');

    Bio::Otter::Git->dump;

    $cl->ua_tell_proxies;

    my $mw = CanvasWindow::MainWindow->new
      ($Bio::Otter::Lace::Client::PFX."Species List");

    # Look for zombies every 5 seconds
    $mw->repeat(
        5000,
        sub {
            while ((my $dead_pid = waitpid(-1, POSIX::WNOHANG)) > 0) {
                my $rc = $?;
                $rc .= ($rc & 127
                        ? ' = signal '.($rc & 127)
                        : ' = rc '.($rc >> 8)) if $rc != 0;
                $logger->warn("Process $dead_pid exited $rc");
            }
        }
    );

    my $SpeciesListWindow = MenuCanvasWindow::SpeciesListWindow->new($mw);
    $SpeciesListWindow->Client($cl);

    my $prompt_sub = sub {
        my ($self) = @_;
        my $user = $self->author;

        my $pass = '';
        my $tl = $mw->Toplevel
          (-title => $Bio::Otter::Lace::Client::PFX.'Enter Password');
        my $pass_win = EditWindow::Password->new($tl);
        $pass_win->prompt_string("Enter web password for '$user'");
        $pass_win->passref(\$pass);
        $pass_win->initialise;
        $pass_win->get_password;
        return $pass;
    };
    my $passwarn_sub = sub {
        my ($client, $msg) = @_;
        warn "passwarn: $msg";
        my $dialog = $mw->DialogBox
          (-title => $Bio::Otter::Lace::Client::PFX.'Problem logging in',
           -buttons => ['Ok'],);
        $dialog->add(qw( Label -justify left -text ), $msg)->pack;
        $dialog->Show;
        return;
    };

    $cl->password_prompt($prompt_sub);
    $cl->password_problem($passwarn_sub);

    if ($cl->no_user_config) {
        $SpeciesListWindow->show_preferences(wait => 1);
    }

    try {
        $cl->get_server_otter_config;
    } catch {
        my $msg = "Cannot start: $_";
        $passwarn_sub->($cl, $msg);
        die $msg;
    };

    $SpeciesListWindow->draw;

    # postpone slow checks, for improved responsiveness
    $mw->after(750, [ $SpeciesListWindow, 'ensure_tools' ]);

    Tk::MainLoop();

    $logger->info("Exiting");

    local $SIG{'TERM'} = 'IGNORE';

    # Sending signal to negative of our PID sends
    # signal to all the processes in our process
    # group, ie: all of the children. This cleans
    # up any stray processes we have launched.
    kill TERM => -$$;

    # NB. we are still here, and so is our logger.  Session recovery
    # may be blocked until we are gone.
}

__END__

=head1 NAME - otterlace

=head1 DESCRIPTION

B<otterlace> is a graphical client for the otter
annotation database that uses a local acedb
database with ZMap and Perl/Tk tools to provide a
gene annotation interface.

=head1 COMMAND LINE

The standard parameters controlled by the
B<Bio::Otter::Lace::Defaults> module can be given
on the command line, but the user will not
usually need to give them.  See its documentation
for details.

=head1 FLAGS

=over 4

=item B<view>

Turns off write access to the database, providing
a safe read-only session.  It is still possible
to edit gene structures, but they will not be
saved to the otter server.

=back

=head1 AUTHOR

Ana Code B<email> anacode@sanger.ac.uk

