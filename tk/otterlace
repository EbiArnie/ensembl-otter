#!/usr/local/bin/perl -w

### otterlace

use strict;
use CanvasWindow::DataSetChooser;
use Bio::Otter::Lace::Defaults;

{
    # Become leader of process group so that "kill -$$" at end works
    setpgrp();
    
    if ($^O eq 'dec_osf') {
        die "\n\n  Sorry, this version of otterlace does not work on Alpha machines\n\n\n";
    }
    
    Bio::Otter::Lace::Defaults::do_getopt() || die $Bio::Otter::Lace::Defaults::GETOPT_ERRSTR . "\n";
    my $cl = Bio::Otter::Lace::Defaults::make_Client();
    $cl->cleanup_log_dir('otterlace');
    $cl->make_log_file('otterlace');

    my $pipe_name = Bio::Otter::Lace::Defaults::pipe_name();

    my $mw = CanvasWindow::MainWindow->new("Choose DataSet [$pipe_name]");
    my $dc = CanvasWindow::DataSetChooser->new($mw);
    $dc->Client($cl);

    my $prompt_sub = sub {
        my $self   = shift;
        my $user   = $self->username();
        my $pass   = '';
        my $dialog = $mw->toplevel->DialogBox(
                                              -title   => "Please enter your password ($user)", 
                                              -buttons => [qw(Ok)], -default_button => 'Ok',
                                              -width   => 100,
                                              );
        my $uframe = $dialog->add('Frame')->pack(-side => 'top');
        $uframe->Label(-text => 'Username: ')->pack(-side => 'left');
        $uframe->Entry(
                       -textvariable => \$user,
                       -width        => 20,
                       -foreground   => 'black',
                       -background   => 'white',
                       -relief       => 'sunken',
                                  #-state        => 'disabled',
                       )->pack(
                               -side => 'left',
                               -expand => 1,
                               -fill => 'x'
                               );
        my $pframe = $dialog->add('Frame')->pack(-side => 'top');
        $pframe->Label(-text => 'Password: ')->pack(-side => 'left');
        $pframe->Entry(
                       -textvariable => \$pass,
                       -show         => '*',
                       -width        => 20,
                       -foreground   => 'black',
                       -background   => 'white',
                       -relief       => 'sunken',
                       )->pack(
                               -side =>'left',
                               -expand => 1,
                               -fill => 'x'
                               );
        $dialog->resizable(1,0);
        my $result = $dialog->Show();
        if($result eq 'Ok'){
            if($user ne $self->username){
                warn "Changing user from '" . $self->username . "' to '$user' \n";
                my $c = Bio::Otter::Lace::Defaults::get_config_list();
                # this gets the last array member which is the command line options Config::IniFiles hash
                $c->[$#$c]->{'client'}->{'author'} = $user;
            }
            $self->password($pass);
        }
        $self = undef;
    };

    $cl->password_prompt($prompt_sub);
    $dc->draw;

    Tk::MainLoop();
    
    print STDERR "Exiting\n";

    local $SIG{'TERM'} = 'IGNORE';
    
    # Sending signal to negative of our PID sends
    # signal to all the processes in our process
    # group, ie: all of the children. This cleans
    # up any stray processes we have launched.
    kill TERM => -$$;
}


__END__

=head1 NAME - otterlace

=head1 DESCRIPTION

B<otterlace> is a graphical client for the otter
annotation database that uses a local acedb
database with xace and perl/Tk tools to provide a
gene annotation interface.

=head1 COMMAND LINE

The standard parameters controlled by the
B<Bio::Otter::Lace::Defaults> module can be given
on the command line, but the user will not
usually need to give them.  See its documentation
for details.

=head1 FLAGS

=over 4

=item B<view>

Turns off write access to the database, providing
a safe read-only session.  It is still possible
to edit gene structures, but they will not be
saved to the otter server.

=back

=head1 AUTHOR

James Gilbert B<email> jgrg@sanger.ac.uk

